apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.diegocast.twitterapp"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        archivesBaseName = "TwitterApp-$versionCode-$versionName"
    }
    buildTypes {
        release {
            manifestPlaceholders = [appName: "TwitterApp"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            manifestPlaceholders = [appName: "TwitterApp Debug"]
            applicationIdSuffix ".debug"
            // Avoid DexIndexOverflowException
            // minifyEnabled true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    productFlavors {
        mock {
            buildConfigField "String", "ENVIRONMENT", "\"development\""
            buildConfigField "String", "APIURL", "\"https://api.twitter.com/\""
        }
        production {
            buildConfigField "String", "ENVIRONMENT", "\"production\""
            buildConfigField "String", "APIURL", "\"https://api.twitter.com/\""
        }
    }
    sourceSets {
        production.java.srcDirs += 'src/common/java'
    }
    // Disable mockRelease
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release') {
            if (variant.getFlavors().get(0).name == 'mock') {
                variant.setIgnore(true)
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    // Support libs
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:support-v4:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:customtabs:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:support-annotations:$support_version"

    // Image loading
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Network
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    compile 'com.google.code.gson:gson:2.8.1'

    // Stetho debug bridge
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // UI
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    compile 'me.henrytao:recycler-pager-adapter:2.1.0'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.2.5'
    compile 'com.21buttons:sticky-headers:0.1.3'

    //Dagger 2
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    compile 'com.google.dagger:dagger:2.11'
    provided 'javax.annotation:jsr250-api:1.0'

    //Mockito
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'

    //Mockito for Instrumentation tests
    mockCompile 'org.mockito:mockito-core:2.8.47'
    mockCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.mockito:mockito-android:2.8.47'

    //Espresso
    androidTestCompile 'com.github.21Buttons:FragmentTestRule:v0.0.8'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-web:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //Multidex
    compile 'com.android.support:multidex:1.0.1'

    //ReactiveX
    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:1.0.1'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:1.0.1'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:1.0.1'

    //Auto value
    provided 'com.google.auto.value:auto-value:1.4.1'
    annotationProcessor 'com.google.auto.value:auto-value:1.4.1'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson:0.4.6'
    provided 'com.ryanharter.auto.value:auto-value-gson:0.4.6'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
    annotationProcessor 'com.gabrielittner.auto.value:auto-value-with:1.0.0'

    //Auto value parcelable
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    //Include all the Twitter APIs
    compile 'com.twitter.sdk.android:twitter:3.0.0'

    //Firebase
    compile 'com.google.firebase:firebase-auth:11.0.2'

    //Circular image view
    compile 'de.hdodenhof:circleimageview:2.1.0'
}

task copyResToMockClasses(type: Copy) {
    from "${projectDir}/src/test/res"
    into "${buildDir}/intermediates/classes/test/mock/debug/res"
}

preBuild.dependsOn(copyResToMockClasses)

apply plugin: 'com.google.gms.google-services'
